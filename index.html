<!DOCTYPE html>
<html><head>
    <title>Monolith</title>
    <meta charset="UTF-8" />
    <style>
     body {
	 margin: 0;
     }
     canvas {
	 cursor: crosshair;
	 display: inline-block;
	 background-color: black;
     }
    </style>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <link href="lib/bootstrap.min.css" rel="stylesheet">
    <script type="text/javascript" src="lib/nunjucks.min.js"></script>
    <script type="text/javascript" src="scripts/raytrace.js"></script>
    <script type="text/javascript" src="scripts/util.js"></script>
    <script type="text/javascript" src="scripts/main.js"></script>
    <script id="vs" type="x-shader/x-vertex">
     attribute vec3 position;

     void main(void){
	 gl_Position = vec4(position, 1.0);
     }
    </script>

    <script id="geometryTemplate" type="x-shader/x-fragment">
     precision mediump float;
     uniform vec2 u_iResolution;
     uniform float u_iGlobalTime;
     uniform int u_selectedObjectId;
     uniform int u_selectedObjectIndex;
     uniform int u_selectedComponentId;
     uniform int u_selectedAxis;
     uniform vec3 u_eye;
     uniform vec3 u_up;
     uniform vec3 u_target;
     uniform float u_fov;


     // from Syntopia http://blog.hvidtfeldts.net/index.php/2015/01/path-tracing-3d-fractals/
     vec2 rand2n(vec2 co, float sampleIndex) {
	 vec2 seed = co * (sampleIndex + 1.0);
	 seed+=vec2(-1,1);
	 // implementation based on: lumina.sourceforge.net/Tutorials/Noise.html
	 return vec2(fract(sin(dot(seed.xy ,vec2(12.9898,78.233))) * 43758.5453),
                     fract(cos(dot(seed.xy ,vec2(4.898,7.23))) * 23421.631));
     }

     const int ID_AXIS_CYLINDER = 100;
     
     const float PI = 3.14159265359;
     const float PI_2 = PI/2.;
     const float EPSILON = 0.001;

     const int MTL_DIFFUSE = 0;
     const int MTL_TRANSPARENT = 1;
     int g_mtl = MTL_DIFFUSE;
     int g_objId = -1;
     int g_objIndex = -1;
     int g_objComponentId = -1;
     bool g_isHit = false;

     vec4 intersectSphere(int objId, int objIndex, int objComponentId, int mtl,
			  vec3 sphereCenter, float radius, 
			  vec3 rayOrigin, vec3 rayDir, vec4 isect){
	 g_isHit = false;
	 vec3 v = rayOrigin - sphereCenter;
	 float b = dot(rayDir, v);
	 float c = dot(v, v) - radius * radius;
	 float d = b * b - c;
	 if(d >= 0.){
             float s = sqrt(d);
             float t = -b - s;
             if(t <= EPSILON) t = -b + s;
             if(EPSILON < t && t < isect.x){
		 vec3 p = (rayOrigin + t * rayDir);
		 g_objId = objId;
		 g_objIndex = objIndex;
		 g_objComponentId = objComponentId;
		 g_mtl = mtl;
		 g_isHit = true;
		 return vec4(t, normalize(p - sphereCenter));
             }
	 }
	 return isect;
     }

     vec4 intersectXYCylinder(int objId, int objIndex, int objComponentId, int mtl,
			      float r, vec3 center,
			      vec3 rayOrigin, vec3 rayDir, vec4 isect){
	 g_isHit = false;
	 rayOrigin = rayOrigin - center;
	 float a = rayDir.x * rayDir.x + rayDir.y * rayDir.y;
	 float b = 2. * ( rayOrigin.x * rayDir.x + rayOrigin.y * rayDir.y);
	 float c = rayOrigin.x * rayOrigin.x + rayOrigin.y * rayOrigin.y - r * r;
	 float d = b * b - 4. * a * c;
	 if(d >= 0.){
	     float s = sqrt(d);
	     float t = (-b - s) / (2. * a);
             if(t <= EPSILON) t = (-b + s) / (2. * a);
             if(EPSILON < t && t < isect.x){
		 vec3 p = (rayOrigin + t * rayDir);
		 g_objId = objId;
		 g_objIndex = objIndex;
		 g_objComponentId = objComponentId;
		 g_mtl = mtl;
		 g_isHit = true;
		 return vec4(t, normalize(vec3(p.xy, 0)));
             }
	 }
	 return isect;
     }

     vec4 intersectYZCylinder(int objId, int objIndex, int objComponentId, int mtl,
			      float r, vec3 center,
			      vec3 rayOrigin, vec3 rayDir, vec4 isect){
	 g_isHit = false;
	 rayOrigin = rayOrigin - center;
	 float a = rayDir.z * rayDir.z + rayDir.y * rayDir.y;
	 float b = 2. * ( rayOrigin.z * rayDir.z + rayOrigin.y * rayDir.y);
	 float c = rayOrigin.z * rayOrigin.z + rayOrigin.y * rayOrigin.y - r * r;
	 float d = b * b - 4. * a * c;
	 if(d >= 0.){
	     float s = sqrt(d);
	     float t = (-b - s) / (2. * a);
             if(t <= EPSILON) t = (-b + s) / (2. * a);
             if(EPSILON < t && t < isect.x){
		 vec3 p = (rayOrigin + t * rayDir);
		 g_objId = objId;
		 g_objIndex = objIndex;
		 g_objComponentId = objComponentId;
		 g_mtl = mtl;
		 g_isHit = true;
		 return vec4(t, normalize(vec3(0, p.yz)));
             }
	 }
	 return isect;
     }

     vec4 intersectXZCylinder(int objId, int objIndex, int objComponentId, int mtl,
			      float r, vec3 center,
			      vec3 rayOrigin, vec3 rayDir, vec4 isect){
	 g_isHit = false;
	 rayOrigin = rayOrigin - center;
	 float a = rayDir.x * rayDir.x + rayDir.z * rayDir.z;
	 float b = 2. * ( rayOrigin.x * rayDir.x + rayOrigin.z * rayDir.z);
	 float c = rayOrigin.x * rayOrigin.x + rayOrigin.z * rayOrigin.z - r * r;
	 float d = b * b - 4. * a * c;
	 if(d >= 0.){
	     float s = sqrt(d);
	     float t = (-b - s) / (2. * a);
             if(t <= EPSILON) t = (-b + s) / (2. * a);
             if(EPSILON < t && t < isect.x){
		 vec3 p = (rayOrigin + t * rayDir);
		 g_objId = objId;
		 g_objIndex = objIndex;
		 g_objComponentId = objComponentId;
		 g_mtl = mtl;
		 g_isHit = true;
		 return vec4(t, normalize(vec3(p.x, 0, p.z)));
             }
	 }
	 return isect;
     }

     // default plane is aligned the z-axis
     vec4 intersectRect (int objId, int objIndex, int objComponentId, int mtl,
			 float distToOrigin, float size, mat3 rotation, mat3 invRotation,
			 vec3 rayOrigin, vec3 rayDir, vec4 isect) {
	 g_isHit = false;
	 vec3 c = vec3(0, 0, distToOrigin);
	 const vec3 defaultN = vec3(0, 0, 1);
	 vec3 n = rotation * defaultN;
	 float d = -dot(rotation * c, n);
	 float v = dot(n, rayDir);
	 float t = -(dot(n, rayOrigin) + d) / v;
	 if(EPSILON < t && t < isect.x){
	     float hSize = size * 0.5;
    	     vec3 p = rayOrigin + t * rayDir;
	     p = invRotation * p;
             if(-hSize < p.x && p.x < hSize &&
		-hSize < p.y && p.y < hSize ){
		 g_objId = objId;
		 g_objIndex = objIndex;
		 g_objComponentId = objComponentId;
		 g_mtl = mtl;
		 g_isHit = true;
        	 return vec4(t, n);
             }
	 }
	 return isect;
     }

     const float PI_4 = 12.566368;
     vec3 diffuseLighting(const vec3 p, const vec3 n, const vec3 diffuseColor,
			  const vec3 lightPos, const vec3 lightPower){
  	 vec3 v = lightPos - p;
  	 float d = dot(n, normalize(v));
  	 float r = length(v);
  	 return (d > 0. 
     // && lightVisibility(p, lightPos) == 1.
         ) ?
    		(lightPower * (abs(d) / (PI_4 * r * r))) * diffuseColor
    	      : vec3(0.0);
     }

     const vec3 lightPos = vec3(900, 450, 0);
     const vec3 lightPower = vec3(1000000.);
     const vec3 ambientFactor = vec3(0.1);
     const int MAX_TRACE_DEPTH = 5;
     const vec3 BLACK = vec3(0);
     const vec3 WHITE = vec3(1);
     const vec3 RED = vec3(1, 0, 0);
     const vec3 GREEN = vec3(0, 1, 0);
     const vec3 BLUE = vec3(0, 0, 1);
     const vec3 YELLOW = vec3(1, 1, 0);
     const vec3 PINK = vec3(1, 0, 1);
     const vec3 LIGHT_BLUE = vec3(0, 1, 1);
     float transparency = 0.8;
     vec3 calcColor(vec3 eye, vec3 ray){
	 vec3 l = BLACK;
	 float coeff = 1.0;
	 for (int depth = 0 ; depth < MAX_TRACE_DEPTH ; depth++){
	     vec4 result = vec4(99999.);
	     
	     result = intersectSphere(0, 0, 0, MTL_DIFFUSE,
				      vec3(0), 1., 
				      eye, ray, result);
	     
	     vec3 matColor = WHITE;

  	     if(result.x != 99999.){
    		 vec3 intersection = eye + ray * result.x;
		 vec3 normal = result.yzw;
		 vec3 diffuse = diffuseLighting(intersection, normal, matColor, lightPos, lightPower);
		 vec3 ambient = matColor * ambientFactor;
		 if(g_mtl == MTL_TRANSPARENT){
		     coeff *= transparency;
		     l += (diffuse + ambient) * coeff;
		     eye = intersection + ray * 0.01;
		     continue;
		 }else{
		     l += (diffuse + ambient) * coeff;
		 }
	     }
	     break;
	 }
  	 return l;
     }

     vec3 hsv2rgb(vec3 c){
	 const vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
	 vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
	 return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
     }


     const float DISPLAY_GAMMA_COEFF = 1. / 2.2;
     vec3 gammaCorrect(vec3 rgb) {
	 return vec3((min(pow(rgb.r, DISPLAY_GAMMA_COEFF), 1.)),
                     (min(pow(rgb.g, DISPLAY_GAMMA_COEFF), 1.)),
                     (min(pow(rgb.b, DISPLAY_GAMMA_COEFF), 1.)));
     }

     vec3 calcRay (const vec3 eye, const vec3 target,
		   const vec3 up, const float fov,
		   const float width, const float height, const vec2 coord){
	 float imagePlane = (height * .5) / tan(radians(fov) * .5);
  	 vec3 v = normalize(target - eye);
 	 vec3 focalXAxis = normalize(cross(v, up));
  	 vec3 focalYAxis =  normalize(cross(v, focalXAxis ));
  	 vec3 center = v * imagePlane;
  	 vec3 origin = center - (focalXAxis * (width  * .5)) - (focalYAxis * (height * .5));
  	 return normalize(origin + (focalXAxis * coord.x) + (focalYAxis * (height - coord.y)));
     }

     const float SAMPLE_NUM = 10.;
     void main(){	 
	 vec4 sum = vec4(0);
	 for(float i = 0. ; i < SAMPLE_NUM ; i++){
             vec2 coordOffset = rand2n(gl_FragCoord.xy, i);
             
             vec3 ray = calcRay(u_eye, u_target, u_up, u_fov,
    		               	u_iResolution.x, u_iResolution.y,
        		        gl_FragCoord.xy + coordOffset);
             
             sum += vec4(gammaCorrect(calcColor(u_eye, ray)), 1.);
	 }
	 gl_FragColor = vec4(sum/SAMPLE_NUM);
     }
    </script>
    <script id="fractalTemplate" type="x-shader/x-fragment">
     precision mediump float;
     uniform vec2 u_iResolution;
     uniform float u_iGlobalTime;
     uniform int u_selectedAxis;
     uniform vec3 u_eye;
     uniform vec3 u_up;
     uniform vec3 u_target;
     uniform float u_fov;
     uniform int u_numIterations;

     uniform float u_boxScale;
     uniform float u_minRadius;
     uniform float u_minRadius2;
     uniform float u_fixedRadius;
     uniform float u_fixedRadius2;
     uniform float u_scale;
     uniform vec3 u_offset;
     
     int g_mtl = -1;
     
     // from Syntopia http://blog.hvidtfeldts.net/index.php/2015/01/path-tracing-3d-fractals/
     vec2 rand2n(vec2 co, float sampleIndex) {
	 vec2 seed = co * (sampleIndex + 1.0);
	 seed+=vec2(-1,1);
	 // implementation based on: lumina.sourceforge.net/Tutorials/Noise.html
	 return vec2(fract(sin(dot(seed.xy ,vec2(12.9898,78.233))) * 43758.5453),
                     fract(cos(dot(seed.xy ,vec2(4.898,7.23))) * 23421.631));
     }
     
     vec3 sphereInvert(vec3 pos, vec3 spherePos, float sphereR, float sphereR2){
	 return ((pos - spherePos) * sphereR2)/(distance(pos, spherePos) * distance(pos, spherePos) ) + spherePos;
     }

     float loopNum = 0.;
     const int MAX_ITERATION = 30;
     float distMandelbox(vec3 pos){
	 loopNum = 0.;
	 vec4 p = vec4(pos.xyz, 1.0);
	 vec4 p0 = p;  // p.w is knighty's DEfactor
	 vec2 scaleFactor = vec2(u_scale, abs(u_scale));
	 for (int i = 0; i < MAX_ITERATION; i++) {
	     if(i > u_numIterations) break;
             p.xyz = clamp(p.xyz, -u_boxScale, u_boxScale) * 2.0 * u_boxScale - p.xyz;  // box fold
             float d = dot(p.xyz, p.xyz);
	     if(d < u_minRadius2){
		 p *= u_fixedRadius2 / u_minRadius2;
	     }else if(d < u_fixedRadius2){
		 p *= u_fixedRadius2 / d;
	     }
             p = p * scaleFactor.xxxy + p0 + vec4(u_offset, 0.0);
	     loopNum = float(i);
	 }
	 return length(p.xyz) / p.w;
     }

     vec3 calcRay (const vec3 eye, const vec3 target, const vec3 up, const float fov,
									   const float width, const float height, const vec2 coord){
	 float imagePlane = (height * .5) / tan(radians(fov) * .5);
	 vec3 v = normalize(target - eye);
	 vec3 xaxis = normalize(cross(v, up));
	 vec3 yaxis =  normalize(cross(v, xaxis));
	 vec3 center = v * imagePlane;
	 vec3 origin = center - (xaxis * (width  *.5)) - (yaxis * (height * .5));
	 return normalize(origin + (xaxis * coord.x) + (yaxis * (height - coord.y)));
     }

     const vec4 K = vec4(1.0, .666666, .333333, 3.0);
     vec3 hsv2rgb(const vec3 c){
	 vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
	 return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
     }

     float distFunc(vec3 p){
	 return distMandelbox(p);
     }

     const vec2 d = vec2(0.01, 0.);
     vec3 getNormal(const vec3 p){
	 return normalize(vec3(distFunc(p + d.xyy) - distFunc(p - d.xyy),
                               distFunc(p + d.yxy) - distFunc(p - d.yxy),
                               distFunc(p + d.yyx) - distFunc(p - d.yyx)));
     }

     const float PI_4 = 12.566368;
     const vec3 LIGHTING_FACT = vec3(0.);
     const vec3 BLACK = vec3(0);
     vec3 diffuseLighting(const vec3 p, const vec3 n, const vec3 diffuseColor,
			  const vec3 lightPos, const vec3 lightPower){
	 vec3 v = lightPos - p;
	 float dot = dot(n, normalize(v));
	 float r = length(v);
	 return (dot > 0.) ?
		(lightPower * (dot / (PI_4 * r * r))) * diffuseColor
	      : BLACK;
     }

     const vec3 lightPos = vec3(400, 400, 400);
     const vec3 lightPos2 = vec3(400., -400., -400);
     const vec3 lightPower = vec3(2000000.);

     const int MAX_MARCHING_LOOP = 800;
     const float NO_HIT = 99999.;
     const float EPSILON = 0.01;
     vec4 march(const vec3 origin, const  vec3 ray,
		const float threshold, vec4 isect){
	 vec3 rayPos = origin;
	 float dist;
	 float rayLength = 0.;
	 for(int i = 0 ; i < MAX_MARCHING_LOOP ; i++){
	     dist = distFunc(rayPos);
	     rayLength += dist;
	     rayPos = origin + ray * rayLength ;
	     if(dist < threshold) break;
	 }
	 if(dist < EPSILON && rayLength < isect.x){
	     return vec4(rayLength);
	 }else{
	     return isect;
	 }
     }

     const vec3 ambientFactor = vec3(0.1);
     vec3 calcColor(vec3 eye, vec3 ray){
	 vec3 l = BLACK;
	 float coeff = 1.;
	 vec4 result = vec4(NO_HIT);
	 result = march(eye, ray, 0.01, result);
	 
	 if(result.x != NO_HIT){
	     vec3 matColor = vec3(1);
	     vec3 normal;
	     vec3 intersection = eye + ray * result.x;
	     normal = getNormal(intersection);
	     matColor = hsv2rgb(vec3(0.1 , 1., 1.));

	     l += diffuseLighting(intersection, normal, matColor, lightPos, lightPower);
	     //	     l += diffuseLighting(intersection, normal, matColor, lightPos2, lightPower);
	     l += matColor * ambientFactor;
	 }
	 return l;
     }

     const float DISPLAY_GAMMA_COEFF = 1. / 2.2;
     vec3 gammaCorrect(vec3 rgb) {
	 return vec3((min(pow(rgb.r, DISPLAY_GAMMA_COEFF), 1.)),
		     (min(pow(rgb.g, DISPLAY_GAMMA_COEFF), 1.)),
		     (min(pow(rgb.b, DISPLAY_GAMMA_COEFF), 1.)));
     }

     const float SAMPLE_NUM = 1.;
     void main(){
	 /* vec4 sum = vec4(0);
	    for(float i = 0. ; i < SAMPLE_NUM ; i++){
	  *     vec2 coordOffset = rand2n(gl_FragCoord.xy, i);
	  *     vec3 ray = calcRay(u_eye, target, u_up, u_fov,
    	    u_iResolution.x, u_iResolution.y,
            gl_FragCoord.xy + coordOffset);
	  *     
	  *     sum += vec4(gammaCorrect(calcColor(u_eye, ray)), 1.);
	    }
	    gl_FragColor = vec4(sum/SAMPLE_NUM);*/
	 const vec2 coordOffset = vec2(0.5);
	 vec3 ray = calcRay(u_eye, u_target, u_up, u_fov,
			    u_iResolution.x, u_iResolution.y,
			    gl_FragCoord.xy + coordOffset);

	 gl_FragColor = vec4(gammaCorrect(calcColor(u_eye, ray)), 1.);
     }
    </script>
</head>
<body>
    <nav class="navbar">
	<div class="container">
            <div class="navbar-header">
		<button type="button" class="navbar-toggle collapsed" data-toggle="collapse" data-target="#navbar" aria-expanded="false" aria-controls="navbar">
		    <span class="sr-only">Toggle navigation</span>
		</button>
		<a class="navbar-brand">Monolith</a>
            </div>
            <div id="navbar" class="collapse navbar-collapse">
		<ul class="nav navbar-nav">
		    <li class="active"><a href="index.html">Home</a></li>
		</ul>
            </div>
	</div>
    </nav>

    <div class="container">
	<div class="row">
	    <div class="col-sm-5 col-md-3 sidebar-offcanvas">
		<div class="well">
		    <ul class="nav">
		    </ul>
		</div>
            </div>
	    <div class="col-sm-7 col-md-9">
	      <div id="content">
		<div class="canvas-area">
		  <h1>Monolith</h1>
		  <canvas id="canvas" width="256" height="256"></canvas>
		  <canvas id="fractalCanvas" width="512" height="512"></canvas>
		  <br>
		</div>
	      </div>
	    </div>
	</div>
    </div>

</body>
</html>
